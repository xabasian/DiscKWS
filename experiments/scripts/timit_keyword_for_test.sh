#!/bin/csh

if ($# != 3) then
    echo "Usgae: $0  <keyword> <mfc files> <output dir>"
    exit
endif

set keyword=$1
set mfc_files=$2
set out_dir=$3

# set timit location
set timit_original_dir=`cat config/timit_location`
#set timit_original_dir=/Corpora/timit
if ( ! -d $timit_original_dir ) then
	echo "TIMIT directory was not found under $timit_original_dir. "
	echo "It was supposed to be generated by scripts/q1-extract_features_and_labels.sh"
endif
	
# TIMIT prompts
set timit_prompts=$timit_original_dir/doc/prompts.txt
if ( ! -e $timit_prompts ) then
	echo "Could not file TIMIT prompts file under $timit_prompts. Maybe TIMIT location was not set properly."
endif
	
# TIMIT dictionary
set timit_dict=$timit_original_dir/doc/timitdic.txt
if ( ! -e $timit_dict ) then
	echo "Could not file TIMIT dictionary file under $timit_dict. Maybe TIMIT location was not set properly."
endif

# output files
set keyword_phonemes="$out_dir/${keyword}.phonemes"
set neg_files="$out_dir/${keyword}.neg_files"
set neg_list="$out_dir/${keyword}.neg_list"
set neg_words="$out_dir/${keyword}.neg_words"
set pos_files="$out_dir/${keyword}.pos_files"
set pos_list="$out_dir/${keyword}.pos_list"
set pos_words="$out_dir/${keyword}.pos_words"
set pos_prompts="$out_dir/${keyword}.pos_prompts"
set neg_prompts="$out_dir/${keyword}.neg_prompts"

# generate phoneme code for the keyword
# and map 61 phoneme code to 39 phoneme code
grep "^$keyword "  $timit_dict  | \
    grep -v ';' | \
    cut -d" " -f 2-99 | \
    tr -d '/[:digit:]' | \
    scripts/phoneme_map_timit61_to_leehon39.pl >! $keyword_phonemes 

# find all prompts with the given keyword
grep -v "^;" $timit_prompts | \
    grep -w -i $keyword |  \
    awk '{print $NF}' | \
    tr -d "()" | \
    egrep -v '(sa[12])' >!  $pos_prompts

# for each prompt find all files from mfc file list
grep -m 20 -w -f $pos_prompts $mfc_files >! $pos_list
cat $pos_list | sed 's/.mfc//g' >!  $pos_files
cat $pos_list | sed 's/.mfc/.words/g' >!  $pos_words

# find all prompts without the keyword
grep -v "^;" $timit_prompts | \
		grep -v -i $keyword | \
    awk '{print $NF}' | \
    tr -d "()" | \
    egrep -v '(sa[12])' >! $neg_prompts

# for each prompt find all files from mfc file list
grep -m 20 -w -f $neg_prompts $mfc_files >! $neg_list
cat $neg_list | sed 's/.mfc//g' >! $neg_files
cat $neg_list | sed 's/.mfc/.words/g' >!  $neg_words
